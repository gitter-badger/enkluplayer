apply plugin: 'com.createar.plugins.unity'
apply plugin: 'maven'
group = 'com.createar.enkluplayer'

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()

        maven {
            url 'https://plugins.gradle.org/m2/'
        }

        maven {
            url "${nexusUrl}/maven-releases/"
            credentials {
                username = nexusUsername
                password = nexusPassword
            }
        }
    }

    dependencies {
        classpath 'com.createar.plugins:unity:0.2.4'
    }
}

// build dirs
def uwpdir = './Builds/Wsa.x86'
def webgldir = './Builds/WebGl'

// wsa
task builduwp_clean(type:Delete) {
    delete uwpdir
}

task builduwp_unity(type:com.createar.plugins.unity.UnityMethodTask) {
    method 'CreateAR.EnkluPlayer.Editor.UnityBuilderHooks.BuildWsaPlayer'
    target 'wsaplayer'
    logPath 'wsa.log'
    unityBin UNITY_BIN
    username UNITY_USERNAME
    password UNITY_PASSWORD
    serial UNITY_SERIAL
}
builduwp_unity.dependsOn = [builduwp_clean]

task builduwp_restore(type:Exec) {
    workingDir uwpdir
    executable nugetAbsPath
    args 'restore'
    standardOutput System.out
}
builduwp_restore.dependsOn = [builduwp_unity]

task builduwp_csc(type:Exec) {
    // msbuild /p:Configuration=Release /p:platform=x86
    workingDir uwpdir
    executable cscPath
    args '/p:Configuration=Release', '/p:Platform=x86'
    standardOutput System.out
}
builduwp_csc.dependsOn = [builduwp_restore]

task builduwp(type:Zip) {
    from("$uwpdir/Enklu/AppPackages") {
        include '**/*.appx'
    }
    archiveName 'uwp.build.zip'
    destinationDir file('.')
}
builduwp.dependsOn = [builduwp_csc];

// webgl
task buildwebgl_clean(type:Delete) {
    delete webgldir
}

task buildwebgl_csc(type:com.createar.plugins.unity.UnityMethodTask) {
    method 'CreateAR.EnkluPlayer.Editor.UnityBuilderHooks.BuildWebGlPlayer'
    target 'webgl'
    logPath 'webgl.log'
    unityBin UNITY_BIN
    username UNITY_USERNAME
    password UNITY_PASSWORD
    serial UNITY_SERIAL
}
buildwebgl_csc.dependsOn = [buildwebgl_clean]

task buildwebgl(type:Zip) {
    from("$webgldir") {
        include '**/*'
    }
    archiveName 'webgl.build.zip'
    destinationDir file('.')
}
buildwebgl.dependsOn = [buildwebgl_csc]

task test_webgl(type:com.createar.plugins.unity.UnityUnitTestTask) {
    target 'webgl'
    logPath 'webgl.log'
    unityBin UNITY_BIN
    username UNITY_USERNAME
    password UNITY_PASSWORD
    serial UNITY_SERIAL
}